import ml.darubyminer360.gradleutils.ChangelogUtils
import ml.darubyminer360.gradleutils.GradleUtils
import ml.darubyminer360.gradleutils.tasks.ExtractTeamCityProjectConfigurationTask
import ml.darubyminer360.gradleutils.tasks.GenerateChangelogTask

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
}

ext {
    GIT_INFO = GradleUtils.gitInfo(file('.'))
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

group 'ml.darubyminer360.gradleutils' //Test
version GradleUtils.getTagOffsetVersion(GIT_INFO)
println('Version: ' + version)

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.10.0.202012080955-r'
}

task copyGradleUtilsGroovy(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/groovy'
    from('buildSrc/src/main/groovy') {
        include '**/*.groovy'
        include '**/*.java'
    }
}

task copyGradleUtilsResources(type: Copy) {
    outputs.upToDateWhen { false }
    into 'src/main/resources'
    from('buildSrc/src/main/resources') {
        include '**/*.zip'
    }
}

compileGroovy.dependsOn(copyGradleUtilsResources)
checkLicenseMain.dependsOn(copyGradleUtilsResources)
compileGroovy.dependsOn(copyGradleUtilsGroovy)
checkLicenseMain.dependsOn(copyGradleUtilsGroovy)

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

license {
    header = file('HEADER')
    ext {
        name = 'GradleUtils'
        year = 2022
        fullname = 'DaRubyMiner360'
    }
    exclude '**/*.properties'
}

gradlePlugin {
    plugins {
        gradleutils {
            id = 'ml.darubyminer360.gradleutils'
            implementationClass = 'ml.darubyminer360.gradleutils.GradleUtilsPlugin'
        }
    }
}

ChangelogUtils.setupChangelogGenerationFromTag(project, '1.0')

publishing {
    publications {
        pluginMaven(MavenPublication) { mavenPublication ->
            artifact sourcesJar

            ChangelogUtils.setupChangelogGenerationForPublishing(project, mavenPublication)

            pom {
                name = 'Gradle Utils'
                description = 'Used by Cloud projects as a util library for Gradle buildscripts'
                url = 'https://github.com/CloudLoaderMC/GradleUtils'
                scm {
                    url = 'https://github.com/CloudLoaderMC/GradleUtils'
                    connection = 'scm:git:git://github.com/CloudLoaderMC/GradleUtils.git'
                    developerConnection = 'scm:git:git@github.com:CloudLoaderMC/GradleUtils.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/CloudLoaderMC/GradleUtils/issues'
                }

                licenses {
                    license {
                        name = 'Apache 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'DaRubyMiner360'
                        name = 'DaRubyMiner360'
                    }
                }
            }
        }
    }
    repositories {
        maven GradleUtils.getPublishingCloudMaven(project)
    }
}

task setupTeamCityProject(type: ExtractTeamCityProjectConfigurationTask) {
}

//GradleUtils.setupCITasks(project);